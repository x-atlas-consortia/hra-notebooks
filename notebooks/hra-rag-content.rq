PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX uberon: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX description1: <http://purl.obolibrary.org/obo/IAO_0000115>
PREFIX description2: <http://purl.obolibrary.org/obo/UBPROP_0000010>
PREFIX bds: <http://www.bigdata.com/rdf/search#>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra>

SELECT DISTINCT ?term ?name ?aka ?description (MAX(?relevance) as ?relevance) (MIN(?rank) as ?rank)
FROM HRA:
WHERE {
  SERVICE <http://www.bigdata.com/rdf/search#search> {
    ?literal bds:search "kidney renal" .
    ?literal bds:rank ?rank .
    ?literal bds:maxRank "1000" .
    ?literal bds:relevance ?relevance .
    ?literal bds:matchAllTerms "false" .
  }
  ?term ?p ?literal .
  FILTER(STRSTARTS(STR(?term), 'http://purl.obolibrary.org'))

  ?term rdfs:label ?name .

  OPTIONAL { 
    SELECT ?term (GROUP_CONCAT(DISTINCT ?akaLabel; SEPARATOR='; ') as ?aka)
    WHERE {
      ?term oboInOwl:hasExactSynonym ?akaLabel .
      FILTER(STRSTARTS(STR(?term), STR(uberon:)))
    }
    GROUP BY ?term
  }

  OPTIONAL { ?term description1: ?description1 . }
  OPTIONAL { ?term description2: ?description2 . }
  BIND(IF(BOUND(?description1), ?description1, IF(BOUND(?description2), ?description2, ?name)) as ?description)
}
GROUP BY ?term ?name ?aka ?description
ORDER BY DESC(?relevance)
